/**
 * File:	include/ipsec/dialogs.ycp
 * Package:	Configuration of ipsec
 * Summary:	Dialogs definitions
 * Authors:	Ludwig Nussel <lnussel@suse.de>
 *
 * $Id$
 */

{

textdomain "ipsec";

import "Label";
import "Wizard";
import "IPsec";

include "ipsec/helps.ycp";

/**
 * Configure1 dialog
 * @return dialog result
 */
any NewConnectionDialog () {

    /* IPsec configure1 dialog caption */
    string caption = _("IPsec Connection Configuration");

    /* IPsec configure1 dialog contents */
    term contents = `VBox(
	`HBox(
	    `HStretch(),
	    `Frame(_("Choose connection type"),
		`RadioButtonGroup(`id(`rbg),
		    `VBox(
			`Left(`RadioButton(`id(`roadwarrior), _("Road Warrior Server"), true)),
			`Left(`RadioButton(`id(`custom), _("Custom")))
		    )
		)
	    ),
	    `HStretch()
	)
    );

    Wizard::SetContentsButtons(caption, contents, HELPS["newconnection"]:"",
	    Label::BackButton(), Label::NextButton());

    any ret = nil;
    while(true) {

	ret = UI::UserInput();

	/* abort? */
	if(ret == `abort || ret == `cancel) {
	    if(ReallyAbort()) break;
	    else continue;
	}
        else if(ret == `next)
	{
	    symbol type = (symbol) UI::QueryWidget(`id(`rbg), `CurrentButton);

	    ret = type;

	    break;
	}
	else if(ret == `back) {
            break;
        }
        else {
            y2error("unexpected retcode: %1", ret);
            continue;
        }
    }

    return ret;
}

string ConnectionNameTextEntryPopup(string headline, string txtlabel, string value)
{
    UI::OpenDialog(
	`VBox(
	    `Heading(headline),
	    `TextEntry(`id(`value), txtlabel, value),
	    `VSpacing(0.5),
	    `HBox(
		`PushButton(`id(`ok), `opt(`default), Label::OKButton()),
		`PushButton(`id(`cancel), Label::CancelButton())
	    )
	)
    );

    if( UI::UserInput() != `ok)
    {
	UI::CloseDialog();
	return nil;
    }

    string value = (string) UI::QueryWidget(`id(`value), `Value);

    UI::CloseDialog();

    return value;
}

any AskConnectionNamePopup()
{
    string headline = _("Enter connection name");
    // connection name
    string txtlabel = _("Name");
    string value = "";

    UI::OpenDialog(
	`VBox(
	    `Heading(headline),
	    `TextEntry(`id(`value), txtlabel, value),
	    `VSpacing(0.5),
	    `HBox(
		`PushButton(`id(`ok), `opt(`default), Label::OKButton()),
		`PushButton(`id(`cancel), Label::CancelButton())
	    )
	)
    );

    UI::ChangeWidget(`id(`value), `ValidChars, "-_0123456789abcdefghijklmnopqrstuvwxyz");
    UI::SetFocus(`id(`value));

    if( UI::UserInput() != `ok)
    {
	UI::CloseDialog();
	return `back;
    }

    string name = (string) UI::QueryWidget(`id(`value), `Value);

    UI::CloseDialog();

    if(size(IPsec::current_connection_name) != 0 && IPsec::current_connection_name != name)
    {
	IPsec::current_connection_oldname = IPsec::current_connection_name;
    }
    IPsec::current_connection_name = name;

    return `next;
}

void ReplaceLeftRightConnectionConfigDialogIdCombo(string cert, string value)
{
    y2error("%1 %2", cert, value);
    list ids = IPsec::getIdsOfCert(cert);
    UI::ReplaceWidget(`id(`idcombo),
	`ComboBox(`id(`ident), `opt(`editable, `hstretch), _("Identification"), ids));
    if(value == "")
	value = ids[0]:"";
    UI::ChangeWidget(`id(`ident), `Value, value );
}

/**
 * Configure2 dialog
 * @param side "left" or "right"
 * @return dialog result
 */
any LeftRightConnectionConfigDialog(
    string iptext,
    string subnettext,
    string side,
    list ips,
    list networks)
{
    list certfiles = IPsec::getCertificateFiles();

    /* IPsec configure2 dialog caption */
    string caption = _("IPsec Configuration");

    /* IPsec configure2 dialog contents */
    term contents = `HBox(
	`HStretch(),
	`VBox(
	    `ComboBox(`id(`addr), `opt(`editable, `hstretch), iptext, ips),
	    `VSpacing(1.5),
	    `VBox(
		`Left(`CheckBox(`id(`isgateway), `opt(`notify), _("Act as Gateway"))),
		`HBox(`HSpacing(3), `ComboBox(`id(`subnet), `opt(`editable, `hstretch), subnettext, networks))
	    ),
	    `VSpacing(2),
	    `HBox(
		`ComboBox(`id(`cert), `opt(`editable, `hstretch, `notify), _("Certificate"), certfiles),
		`PushButton(`id(`import), _("Import..."))
	    ),
	    `ReplacePoint(`id(`idcombo), `Empty())
	),
	`HStretch()
    );

    Wizard::SetContentsButtons(caption, contents, HELPS["c2"]:"",
	    Label::BackButton(), Label::NextButton());

    string tmp = IPsec::current_connection[side]:"";
    if(side == "left" && tmp == "" )
	tmp = "%defaultroute";

    if(tmp != "")
	UI::ChangeWidget(`id(`addr), `Value, tmp );

    UI::ChangeWidget(`id(`subnet), `Value, IPsec::current_connection[side+"subnet"]:"" );

    tmp = IPsec::current_connection[side+"cert"]:"";
    if (side == "left" && tmp == "")
    {
	tmp = certfiles[0]:"";
    }
    if (side == "right" || tmp != "")
	UI::ChangeWidget(`id(`cert), `Value, tmp );

    ReplaceLeftRightConnectionConfigDialogIdCombo(tmp, IPsec::current_connection[side+"id"]:"");

    if(IPsec::current_connection[side+"subnet"]:"" == "")
    {
	UI::ChangeWidget(`id(`isgateway), `Value, false );
	UI::ChangeWidget(`id(`subnet), `Enabled, false );
    }
    else
	UI::ChangeWidget(`id(`isgateway), `Value, true );

    UI::SetFocus(`id(`addr));

    any ret = nil;
    while(true) {

	ret = UI::UserInput();

	/* abort? */
	if(ret == `abort || ret == `cancel) {
	    if(ReallyAbort()) break;
	    else continue;
	}
        else if(ret == `next || ret == `back || ret == `import)
	{
	    string addr = (string) UI::QueryWidget(`id(`addr), `Value);
	    if(ret == `next && size(addr)==0)
	    {
		Popup::Message(_("You must set an Address"));
		UI::SetFocus(`id(`addr));
		continue;
	    }

	    boolean gw = (boolean) UI::QueryWidget(`id(`isgateway), `Value);
	    string subnet = (string) UI::QueryWidget(`id(`subnet), `Value);
	    if(ret == `next && gw == true && size(subnet)==0)
	    {
		Popup::Message(_("You must set a subnet address if you configure a gateway"));
		UI::SetFocus(`id(`subnet));
		continue;
	    }

	    string cert = (string) UI::QueryWidget(`id(`cert), `Value);
	    string ident = (string) UI::QueryWidget(`id(`ident), `Value);

	    if(ret == `next && size(cert)==0 && side == "left")
	    {
		Popup::Message(_("You must select a certificate"));
		UI::SetFocus(`id(`cert));
		continue;
	    }

	    IPsec::current_connection[side] = addr;
	    IPsec::current_connection[side+"subnet"] = subnet;
	    IPsec::current_connection[side+"cert"] = cert;
	    IPsec::current_connection[side+"id"] = ident;

            break;
        }
	else if(ret == `isgateway)
	{
	    boolean gw = (boolean) UI::QueryWidget(`id(`isgateway), `Value);
	    UI::ChangeWidget(`id(`subnet), `Enabled, gw );
	}
	else if(ret == `cert)
	{
	    string cert = (string) UI::QueryWidget(`id(`cert), `Value);
	    string ident = (string) UI::QueryWidget(`id(`ident), `Value);
	    ReplaceLeftRightConnectionConfigDialogIdCombo(cert, ident);
	}
        else {
            y2error("unexpected retcode: %1", ret);
            continue;
        }
    }

    return ret;
}

any LeftConnectionConfigDialog ()
{
    list ips = IPsec::getIPaddresses();
    ips = add(ips, "%defaultroute");

    list networks = IPsec::getNetworks();

    networks = add(networks, "0.0.0.0/0");

    return LeftRightConnectionConfigDialog(
	_("Local IP Address"),
	_("Local Subnet"),
	"left",
	ips, networks);
}

any RightConnectionConfigDialog ()
{
    list ips = [ "%any" ];

    list networks = [ "0.0.0.0/0" ];

    return LeftRightConnectionConfigDialog(
	_("Remote IP Address"),
	_("Remote Subnet"),
	"right",
	ips, networks);
}

any ConnectionStartModeDialog () {

    /* IPsec configure1 dialog caption */
    string caption = _("IPsec Connection Configuration");
    list bootmodes = IPsec::getBootModeItems();

    /* IPsec configure1 dialog contents */
    term contents = `HBox(
	`HStretch(),
	`VBox(
	    `ComboBox(`id(`bootmode), `opt(`hstretch), _("During Boot ..."), bootmodes ),
	    `Frame(_("Dynamic start"), `RadioButtonGroup(`id(`rbg), `opt(`notify),
		`VBox(
		    `Left(`CheckBox(`id(`nodyncbx), `opt(`notify), _("Start dynamically"))),
		    `ComboBox(`id(`netif), `opt(`editable), _("Interface"), IPsec::getDevices())
		    )
		)
	    )
	),
	`HStretch()
    );

    Wizard::SetContentsButtons(caption, contents, HELPS["c1"]:"",
	    Label::BackButton(), Label::NextButton());

    {
	string val = IPsec::current_connection["auto"]:"";
	if(val != "")
	    UI::ChangeWidget(`id(`bootmode), `Value, IPsec::getBootModeText(val) );
    }

    {
	list<string> devs = IPsec::getNetworkDevicesUP();
	if(size(devs) != 0)
	{
	    UI::ChangeWidget(`id(`netif), `Value, mergestring(devs, " "));
	    UI::ChangeWidget(`id(`netif), `Enabled, true );
	    UI::ChangeWidget(`id(`nodyncbx), `Value, true );
	}
	else
	    UI::ChangeWidget(`id(`netif), `Enabled, false );
    }


    any ret = nil;
    while(true) {

	ret = UI::UserInput();

	/* abort? */
	if(ret == `abort || ret == `cancel)
	{
	    if(ReallyAbort()) break;
	    else continue;
	}
	else if(ret == `nodyncbx)
	{
	    boolean netif = (boolean) UI::QueryWidget(`id(`nodyncbx), `Value);
	    UI::ChangeWidget(`id(`netif), `Enabled, netif );
	}
        else if(ret == `next || ret == `back)
	{
	    string mode = (string) UI::QueryWidget(`id(`bootmode), `Value);
	    IPsec::current_connection["auto"] = mode;

	    if((boolean) UI::QueryWidget(`id(`nodyncbx), `Value))
	    {
		string devs = (string) UI::QueryWidget(`id(`netif), `Value);
		y2debug("devs=%1", devs);
		IPsec::setNetworkDevicesUP(splitstring(devs, " "));
	    }

            break;
        }
        else
	{
            y2error("unexpected retcode: %1", ret);
            continue;
        }
    }

    return ret;
}


/**
 * Dialog for global FreeS/WAN settings
 * @return dialog result
 */
any FreeSWANSettingsDialog ()
{
    string caption = _("FreeS/WAN settings");

    term contents = `HBox (`HStretch(),
	`Frame(IPsec::settings_str,
	    `VBox(
		`Left(`CheckBox(`id(`startatboot), IPsec::start_str, IPsec::getStart())),
		`Left(`CheckBox(`id(`nattraversal), IPsec::nattraversal_str, IPsec::getNatTraversal())),
		`Left(`CheckBox(`id(`strictcrlpolicy), IPsec::strictcrlpolicy_str, IPsec::getStrictCRLPolicy())),
		`Left(`IntField(`id(`crlcheckinterval), "CRL check interval", 0, 99999, IPsec::getCrlCheckInterval()))
	    )
	), `HStretch()
    );

    Wizard::SetContentsButtons(caption, contents, HELPS["freeswansettings"]:"",
	    Label::BackButton(), Label::NextButton());

    // TODO

    any ret = nil;
    while(true) {

	ret = UI::UserInput();

	/* abort? */
	if(ret == `abort || ret == `cancel) {
	    if(ReallyAbort()) break;
	    else continue;
	}
        else if(ret == `next || ret == `back) {
	    boolean startatboot = (boolean) UI::QueryWidget(`id(`startatboot), `Value);
	    boolean nattraversal = (boolean) UI::QueryWidget(`id(`nattraversal), `Value);
	    boolean strictcrlpolicy = (boolean) UI::QueryWidget(`id(`strictcrlpolicy), `Value);
	    integer crlcheckinterval = (integer) UI::QueryWidget(`id(`crlcheckinterval), `Value);

	    IPsec::setStart(startatboot);
	    IPsec::setNatTraversal(nattraversal);
	    IPsec::setStrictCRLPolicy(strictcrlpolicy);
	    IPsec::setCrlCheckInterval(crlcheckinterval);
            break;
        }
        else {
            y2error("unexpected retcode: %1", ret);
            continue;
        }
    }

    return ret;
}

any ConnectionConfigEncryptionDialog () {

    /* IPsec configure1 dialog caption */
    string caption = _("IPsec Connection Configuration");

    /* IPsec configure1 dialog contents */
    term contents = `HBox(
	`HStretch(),
	`VBox(
	    `HBox(
		`Label(_("Encryption Algorithms")),
		`Label(`id(`encalgos), `opt(`outputField, `hstretch), "aes,blowfish,3des"),
		`PushButton(`id(`encalgobutton), _("Choose..."))
	    ),
	    `HBox(
		`Label(_("Authentication Algorithms")),
		`Label(`id(`authalgo), `opt(`outputField, `hstretch), ""),
		`PushButton(`id(`authalgobutton), _("Choose..."))
	    ),
	    /** Translator: you probably don't want to translate this, at least not PFS ;-) */
	    `CheckBox(_("Perfect Forward Secrecy (PFS)"))
	),
	`HStretch()
    );

    Wizard::SetContentsButtons(caption, contents, HELPS["c1"]:"",
	    Label::BackButton(), Label::NextButton());

    any ret = nil;
    while(true) {

	ret = UI::UserInput();

	/* abort? */
	if(ret == `abort || ret == `cancel) {
	    if(ReallyAbort()) break;
	    else continue;
	}
        else if(ret == `next || ret == `back) {
            break;
        }
        else if(ret == `encalgos)
	{
	    break;
	}
        else {
            y2error("unexpected retcode: %1", ret);
            continue;
        }
    }

    return ret;
}


any ConnectionDoneDialog ()
{
    IPsec::commitChangedConnection();
    return `next;
}

/**
 * Configure1 dialog
 * @return dialog result
 */
any EasyRoadWarriorDialog () {

    /* Roadwarrior Dialog caption */
    string caption = _("Create Roadwarrior Connection");

    list certfiles = IPsec::getCertificateFiles();

    list networks = IPsec::getNetworks();

    networks = sort(add(networks, "0.0.0.0/0"));

    IPsec::newRoadWarriorConnection();

    term contents = `VBox(
	`HBox(
	    `HStretch(),
	    `Frame(_("Road Warrior Settings"),
		`VBox(
		    `Left(`CheckBox(`id(`isgateway), `opt(`notify), _("Act as Gateway"), true)),
		    `Left(`HBox(`HSpacing(3), `ComboBox(`id(`subnet), `opt(`editable), _("Network"), networks))),
		    `Left(`ComboBox(`id(`cert), _("Certificate"), certfiles))
		)
	    ),
	    `HStretch()
	)
    );

    Wizard::SetContentsButtons(caption, contents, HELPS["easyroadwarrior"]:"",
	    Label::BackButton(), Label::NextButton());

    if(size(certfiles) == 0)
    {
	if(Popup::ContinueCancel(_("You need to import certificates first")))
	    return `needcert;
	return `back;
    }

    UI::SetFocus(`id(`isgateway));

    any ret = nil;
    while(true)
    {
	ret = UI::UserInput();

	/* abort? */
	if(ret == `abort || ret == `cancel) {
	    if(ReallyAbort()) break;
	    else continue;
	}
        else if(ret == `next)
	{
	    boolean gw = (boolean) UI::QueryWidget(`id(`isgateway), `Value);
	    string subnet = (string) UI::QueryWidget(`id(`subnet), `Value);
	    if(gw == true && size(subnet)==0)
	    {
		Popup::Message(_("You must set a subnet address if you configure a gateway"));
		UI::SetFocus(`id(`subnet));
		continue;
	    }

	    string cert = (string) UI::QueryWidget(`id(`cert), `Value);
	    list ids = IPsec::getIdsOfCert(cert);

	    IPsec::current_connection["leftcert"] = cert;
	    // well, just use first one. should be DN
	    IPsec::current_connection["leftid"] = ids[0]:"";
	    IPsec::current_connection["leftsubnet"] = subnet;

	    IPsec::commitChangedConnection();
	    break;
	}
	else if(ret == `back) {
	    IPsec::setNoConnection();
            break;
        }
	else if(ret == `isgateway)
	{
	    boolean gw = (boolean) UI::QueryWidget(`id(`isgateway), `Value);
	    UI::ChangeWidget(`id(`subnet), `Enabled, gw );
	}
        else {
            y2error("unexpected retcode: %1", ret);
            continue;
        }
    }

    return ret;
}

/* EOF */
}
