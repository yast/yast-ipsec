/**
 * File:	include/ipsec/dialogs.ycp
 * Package:	Configuration of ipsec
 * Summary:	Dialogs definitions
 * Authors:	Ludwig Nussel <lnussel@suse.de>
 *
 * $Id$
 */

{

textdomain "ipsec";

import "Label";
import "Wizard";
import "IPsec";

include "ipsec/helps.ycp";

/**
 * Configure1 dialog
 * @return dialog result
 */
any ConnectionConfigDialog1 () {

    /* IPsec configure1 dialog caption */
    string caption = _("IPsec Connection Configuration");

    /* IPsec configure1 dialog contents */
    term contents = `VBox(
	`HBox(
	    `HStretch(),
	    `TextEntry(`id(`name), _("Connection Name"), IPsec::current_connection_name),
	    `HStretch()
	),
	`VSpacing(2),
	`PushButton(`id(`expert), _("Expert ..."))
    );

    Wizard::SetContentsButtons(caption, contents, HELPS["c1"]:"",
	    Label::BackButton(), Label::NextButton());

    UI::ChangeWidget(`id(`name), `ValidChars, "-_0123456789abcdefghijklmnopqrstuvwxyz");

    UI::SetFocus(`id(`name));

    any ret = nil;
    while(true) {

	ret = UI::UserInput();

	/* abort? */
	if(ret == `abort || ret == `cancel) {
	    if(ReallyAbort()) break;
	    else continue;
	}
        else if(ret == `next || ret == `expert)
	{
	    string name = (string) UI::QueryWidget(`id(`name), `Value);
	    //TODO ensure name is valid
	    if(size(name)==0)
	    {
		Popup::Message(_("Please fill in a name"));
		UI::SetFocus(`id(`name));
		continue;
	    }

	    if(size(IPsec::current_connection_name) != 0 && IPsec::current_connection_name != name)
	    {
		IPsec::current_connection_oldname = IPsec::current_connection_name;
	    }
	    IPsec::current_connection_name = name;
	    break;
	}
	else if(ret == `back) {
            break;
        }
        else {
            y2error("unexpected retcode: %1", ret);
            continue;
        }
    }

    return ret;
}

void ReplaceLeftRightConnectionConfigDialogIdCombo(string cert, string value)
{
    y2error("%1 %2", cert, value);
    list ids = IPsec::getIdsOfCert(cert);
    UI::ReplaceWidget(`id(`idcombo),
	`ComboBox(`id(`ident), `opt(`editable, `hstretch), _("Identification"), ids));
    if(value == "")
	value = ids[0]:"";
    UI::ChangeWidget(`id(`ident), `Value, value );
}

/**
 * Configure2 dialog
 * @param side "left" or "right"
 * @return dialog result
 */
any LeftRightConnectionConfigDialog(
    string iptext,
    string subnettext,
    string side,
    list ips,
    list networks)
{
    list certfiles = IPsec::getCertificateFiles();

    /* IPsec configure2 dialog caption */
    string caption = _("IPsec Configuration");

    /* IPsec configure2 dialog contents */
    term contents = `HBox(
	`HStretch(),
	`VBox(
	    `ComboBox(`id(`addr), `opt(`editable, `hstretch), iptext, ips),
	    `VSpacing(1.5),
	    `VBox(
		`Left(`CheckBox(`id(`isgateway), `opt(`notify), _("Act as Gateway"))),
		`HBox(`HSpacing(3), `ComboBox(`id(`subnet), `opt(`editable, `hstretch), subnettext, networks))
	    ),
	    `VSpacing(2),
	    `HBox(
		`ComboBox(`id(`cert), `opt(`editable, `hstretch, `notify), _("Certificate"), certfiles),
		`PushButton(`id(`certbutton), _("Import..."))
	    ),
	    `ReplacePoint(`id(`idcombo), `Empty())
	),
	`HStretch()
    );

    Wizard::SetContentsButtons(caption, contents, HELPS["c2"]:"",
	    Label::BackButton(), Label::NextButton());

    string tmp = IPsec::current_connection[side]:"";
    if(side == "left" && tmp == "" )
	tmp = "%defaultroute";

    UI::ChangeWidget(`id(`addr), `Value, tmp );
    UI::ChangeWidget(`id(`subnet), `Value, IPsec::current_connection[side+"subnet"]:"" );

    tmp = IPsec::current_connection[side+"cert"]:"";
    if (side == "left" && tmp == "")
    {
	tmp = certfiles[0]:"";
    }
    if (side == "right" || tmp != "")
	UI::ChangeWidget(`id(`cert), `Value, tmp );

    ReplaceLeftRightConnectionConfigDialogIdCombo(tmp, IPsec::current_connection[side+"id"]:"");
    y2error("asfsd");

    if(IPsec::current_connection[side+"subnet"]:"" == "")
    {
	UI::ChangeWidget(`id(`isgateway), `Value, false );
	UI::ChangeWidget(`id(`subnet), `Enabled, false );
    }
    else
	UI::ChangeWidget(`id(`isgateway), `Value, true );

    UI::SetFocus(`id(`addr));

    any ret = nil;
    while(true) {

	ret = UI::UserInput();

	/* abort? */
	if(ret == `abort || ret == `cancel) {
	    if(ReallyAbort()) break;
	    else continue;
	}
        else if(ret == `next || ret == `back)
	{
	    string addr = (string) UI::QueryWidget(`id(`addr), `Value);
	    if(ret == `next && size(addr)==0)
	    {
		Popup::Message(_("You must set an Address"));
		UI::SetFocus(`id(`addr));
		continue;
	    }

	    boolean gw = (boolean) UI::QueryWidget(`id(`isgateway), `Value);
	    string subnet = (string) UI::QueryWidget(`id(`subnet), `Value);
	    if(ret == `next && gw == true && size(subnet)==0)
	    {
		Popup::Message(_("You must set a subnet address if you configure a gateway"));
		UI::SetFocus(`id(`subnet));
		continue;
	    }

	    string cert = (string) UI::QueryWidget(`id(`cert), `Value);
	    string ident = (string) UI::QueryWidget(`id(`ident), `Value);

	    if(ret == `next && size(cert)==0 && side == "left")
	    {
		Popup::Message(_("You must select a certificate"));
		UI::SetFocus(`id(`cert));
		continue;
	    }

	    IPsec::current_connection[side] = addr;
	    IPsec::current_connection[side+"subnet"] = subnet;
	    IPsec::current_connection[side+"cert"] = cert;
	    IPsec::current_connection[side+"id"] = ident;

            break;
        }
	else if(ret == `isgateway)
	{
	    boolean gw = (boolean) UI::QueryWidget(`id(`isgateway), `Value);
	    UI::ChangeWidget(`id(`subnet), `Enabled, gw );
	}
	else if(ret == `cert)
	{
	    string cert = (string) UI::QueryWidget(`id(`cert), `Value);
	    string ident = (string) UI::QueryWidget(`id(`ident), `Value);
	    ReplaceLeftRightConnectionConfigDialogIdCombo(cert, ident);
	}
        else {
            y2error("unexpected retcode: %1", ret);
            continue;
        }
    }

    return ret;
}

any LeftConnectionConfigDialog ()
{
    list ips = IPsec::getIPaddresses();
    ips = add(ips, "%defaultroute");

    list networks = IPsec::getNetworks();

    networks = add(networks, "0.0.0.0/0");

    return LeftRightConnectionConfigDialog(
	_("Local IP Address"),
	_("Local Subnet"),
	"left",
	ips, networks);
}

any RightConnectionConfigDialog ()
{
    list ips = [ "%any" ];

    list networks = [ "0.0.0.0/0" ];

    return LeftRightConnectionConfigDialog(
	_("Remote IP Address"),
	_("Remote Subnet"),
	"right",
	ips, networks);
}

any ConnectionStartModeDialog () {

    /* IPsec configure1 dialog caption */
    string caption = _("IPsec Connection Configuration");
    list bootmodes = IPsec::getBootModeItems();

    /* IPsec configure1 dialog contents */
    term contents = `HBox(
	`HStretch(),
	`VBox(
	    `ComboBox(`id(`bootmode), `opt(`hstretch), _("During Boot ..."), bootmodes ),
	    `Frame(_("Dynamic start"), `RadioButtonGroup(`id(`rbg), `opt(`notify),
		`VBox(
		    `Left(`RadioButton(`id(`nodynrb), `opt(`notify), _("No dynamic start"), true)),
		    `Left(`VBox(
			`RadioButton(`id(`netifrb), `opt(`notify), _("Start after Network interface")),
			`ComboBox(`id(`netif), `opt(`editable), _("Interface"), IPsec::getDevices())
		    )),
		    `Left(`RadioButton(`id(`internetrb), `opt(`notify), _("Start at Internet dial-up")))
		)
	    ))
	),
	`HStretch()
    );

    Wizard::SetContentsButtons(caption, contents, HELPS["c1"]:"",
	    Label::BackButton(), Label::NextButton());

    {
	string val = IPsec::current_connection["auto"]:"";
	if(val != "")
	    UI::ChangeWidget(`id(`bootmode), `Value, IPsec::getBootModeText(val) );
    }

    UI::ChangeWidget(`id(`netif), `Enabled, false );

    any ret = nil;
    while(true) {

	ret = UI::UserInput();

	boolean netif = (boolean) UI::QueryWidget(`id(`netifrb), `Value);
	UI::ChangeWidget(`id(`netif), `Enabled, netif );

	/* abort? */
	if(ret == `abort || ret == `cancel) {
	    if(ReallyAbort()) break;
	    else continue;
	}
        else if(ret == `next || ret == `back) {
	    string mode = (string) UI::QueryWidget(`id(`bootmode), `Value);
	    IPsec::current_connection["auto"] = mode;
            break;
        }
        else {
            y2error("unexpected retcode: %1", ret);
            continue;
        }
    }

    return ret;
}


/**
 * Dialog for global FreeS/WAN settings
 * @return dialog result
 */
any FreeSWANSettingsDialog ()
{
    string caption = _("FreeS/WAN settings");

    term contents = `HBox (`HStretch(),
	`Frame(IPsec::settings_str,
	    `VBox(
		`Left(`CheckBox(`id(`startatboot), IPsec::start_str, IPsec::start)),
		`Left(`CheckBox(`id(`nattraversal), IPsec::nattraversal_str, IPsec::nattraversal)),
		`Left(`CheckBox(`id(`strictcrlpolicy), IPsec::strictcrlpolicy_str, IPsec::strictcrlpolicy)),
		`Left(`IntField(`id(`crlcheckinterval), "CRL check interval", 0, 99999, IPsec::crlcheckinterval))
	    )
	), `HStretch()
    );

    Wizard::SetContentsButtons(caption, contents, HELPS["freeswansettings"]:"",
	    Label::BackButton(), Label::NextButton());

    // TODO

    any ret = nil;
    while(true) {

	ret = UI::UserInput();

	/* abort? */
	if(ret == `abort || ret == `cancel) {
	    if(ReallyAbort()) break;
	    else continue;
	}
        else if(ret == `next || ret == `back) {
	    boolean startatboot = (boolean) UI::QueryWidget(`id(`startatboot), `Value);
	    boolean nattraversal = (boolean) UI::QueryWidget(`id(`nattraversal), `Value);
	    boolean strictcrlpolicy = (boolean) UI::QueryWidget(`id(`strictcrlpolicy), `Value);
	    integer crlcheckinterval = (integer) UI::QueryWidget(`id(`crlcheckinterval), `Value);

	    IPsec::setStart(startatboot);
	    IPsec::setNatTraversal(nattraversal);
	    IPsec::setstrictcrlpolicy(strictcrlpolicy);
	    IPsec::setCrlCheckInterval(crlcheckinterval);
            break;
        }
        else {
            y2error("unexpected retcode: %1", ret);
            continue;
        }
    }

    return ret;
}

any ConnectionConfigEncryptionDialog () {

    /* IPsec configure1 dialog caption */
    string caption = _("IPsec Connection Configuration");

    /* IPsec configure1 dialog contents */
    term contents = `HBox(
	`HStretch(),
	`VBox(
	    `HBox(
		`Label(_("Encryption Algorithms")),
		`Label(`id(`encalgos), `opt(`outputField, `hstretch), "aes,blowfish,3des"),
		`PushButton(`id(`encalgobutton), _("Choose..."))
	    ),
	    `HBox(
		`Label(_("Authentication Algorithms")),
		`Label(`id(`authalgo), `opt(`outputField, `hstretch), ""),
		`PushButton(`id(`authalgobutton), _("Choose..."))
	    ),
	    /** Translator: you probably don't want to translate this, at least not PFS ;-) */
	    `CheckBox(_("Perfect Forward Secrecy (PFS)"))
	),
	`HStretch()
    );

    Wizard::SetContentsButtons(caption, contents, HELPS["c1"]:"",
	    Label::BackButton(), Label::NextButton());

    any ret = nil;
    while(true) {

	ret = UI::UserInput();

	/* abort? */
	if(ret == `abort || ret == `cancel) {
	    if(ReallyAbort()) break;
	    else continue;
	}
        else if(ret == `next || ret == `back) {
            break;
        }
        else if(ret == `encalgos)
	{
	    break;
	}
        else {
            y2error("unexpected retcode: %1", ret);
            continue;
        }
    }

    return ret;
}


any ConnectionDoneDialog ()
{
    IPsec::commitChangedConnection();
    return `next;
}


/* EOF */
}
