/**
 * File:	include/ipsec/complex.ycp
 * Package:	Configuration of ipsec
 * Summary:	Dialogs definitions
 * Authors:	Ludwig Nussel <lnussel@suse.de>
 *
 * $Id$
 */

{

textdomain "ipsec";

import "Label";
import "Popup";
import "Wizard";
import "Wizard_hw";
import "IPsec";
import "IPsecConfig";

include "ipsec/helps.ycp";

/**
 * Return a modification status
 * @return true if data was modified
 */
boolean Modified() {
    return IPsec::Modified();
}

boolean ReallyAbort() {
    return !IPsec::Modified() || Popup::ReallyAbort(true);
}

boolean PollAbort() {
    return UI::PollInput() == `abort;
}

/**
 * Read settings dialog
 * @return `abort if aborted and `next otherwise
 */
symbol ReadDialog() {
    Wizard::RestoreHelp(HELPS["read"]:"");
    // IPsec::AbortFunction = PollAbort;
    boolean ret = IPsec::Read();
    return ret ? `next : `abort;
}

/**
 * Write settings dialog
 * @return `abort if aborted and `next otherwise
 */
symbol WriteDialog() {
    Wizard::RestoreHelp(HELPS["write"]:"");
    IPsec::AbortFunction = PollAbort;
    boolean ret = IPsec::Write();
    return ret ? `next : `abort;
}

term SummaryRichText(string summary)
{
    return `HBox(
	`HSpacing(1.5),
	`VBox(
	    `VSpacing(0.3),
	    `RichText(`id(`the_richtext), summary),
	    `HBox(
		`Right(`PushButton(`id("settings"), `opt (`key_F2), _("Settings..."))),
		`Right(`PushButton(`id("certificates"), `opt (`key_F3), _("C&ertificates..."))),
		`Right(`PushButton(`id("connections"), `opt (`key_F4), _("C&onnections...")))
	    ),
	    `VSpacing(0.4)
	),
	`HSpacing(1.5)
    );
}

/**
 * Summary dialog
 * @return dialog result
 */
any SummaryDialog() {

    /* IPsec summary dialog caption */
    string caption = _("IPsec Configuration");

    list summary = IPsec::Summary();
    list unconfigured = summary[1]:[];
    string configured = summary[0]:"";

    /* Frame label */
//    term contents = Wizard_hw::DetectedContent(_("IPsec to Configure"),
//	    unconfigured, false, configured);

    term contents = SummaryRichText(configured);

    Wizard::SetContentsButtons(caption, contents, HELPS["summary"]:"",
	    Label::BackButton(), Label::FinishButton());

    UI::SetFocus(`id(`the_richtext));

    if(!IPsec::didaskaboutemptycertificates && !IPsec::haveCertificates())
    {
	IPsec::didaskaboutemptycertificates = true;
	if(Popup::YesNo(
	    _("There are no certificates imported yet.\nDo you want to import certificates now?")) == true)
	{
	    return `import;
	}
    }

    if(!IPsec::didaskaboutemptyconnections && !IPsec::haveConnections())
    {
	IPsec::didaskaboutemptyconnections = true;
	if(Popup::YesNo(
	    _("There are no connections defined yet.\nDo you want to define connections now?")) == true)
	{
	    return `configure;
	}
    }

    any ret = nil;
    while(true) {

	ret = UI::UserInput();

	/* abort? */
	if(ret == `abort || ret == `cancel || ret == `back) {
	    if(ReallyAbort()) break;
	    else continue;
	}
        /* overview dialog */
        else if(ret == `edit_button) {
            ret = `overview;
            break;
        }
        /* configure the selected device */
        else if(ret == `configure_button) {
	    // TODO FIXME: check for change of the configuration
            any selected = UI::QueryWidget(`id(`detected_selbox), `CurrentItem);
            if(selected == `other) {
                ret = `other;
            }
            else {
                ret = `configure;
            }
            break;
        }
        else if(ret == `next) {
            break;
        }
        else if(ret == "settings") {
	    ret = `settings;
            break;
        }
        else if(ret == "connections") {
	    ret = `overview;
            break;
        }
        else if(ret == "certificates") {
	    ret = `certificates;
            break;
        }
        else {
            y2error("unexpected retcode: %1", ret);
            continue;
        }
    }

    return ret;
}

void OverviewDialogCreate()
{
    /* IPsec overview dialog caption */
    string caption = _("IPsec Connection Overview");

    list overview = IPsec::Overview();
    term buttons = `HBox(
	`PushButton(`id(`export_button), _("Export")),
	`PushButton(`id(`expert_button), _("Expert ..."))
	);

    term contents = Wizard_hw::ConfiguredContent(
	/* Table header */
	`header(_("Connection"), _("Local"), _("Remote")),
	overview, nil, nil,
	    buttons, nil );

    contents = Wizard_hw::SpacingAround(contents, 1.5, 1.5, 1.0, 1.0);

    Wizard::SetContentsButtons(caption, contents, HELPS["overview"]:"",
	    Label::BackButton(), Label::NextButton());
}

/**
 * Overview dialog
 * @return dialog result
 */
any OverviewDialog()
{
    OverviewDialogCreate();

    UI::SetFocus(`id(`add_button));

    any ret = nil;
    while(true) {

	ret = UI::UserInput();

	/* abort? */
	if(ret == `abort || ret == `cancel) {
	    if(ReallyAbort()) break;
	    else continue;
	}
        /* add */
        else if(ret == `add_button) {
            ret = `add;
	    string name = (string) UI::QueryWidget(`id(`table), `CurrentItem);
	    IPsec::newConnection();
            break;
        }
        /* edit */
        else if(ret == `edit_button) {
            ret = `edit;

	    string name = (string) UI::QueryWidget(`id(`table), `CurrentItem);
	    if(!IPsec::setCurrentConnection(name))
		continue;

            break;
        }
        /* delete */
        else if(ret == `delete_button)
	{
	    string conn = (string) UI::QueryWidget(`id(`table), `CurrentItem);
	    IPsec::DeleteConnection(conn);
	    OverviewDialogCreate();
            continue;
        }
        /* expert */
        else if(ret == `expert_button) {
            ret = `expert;

	    string name = (string) UI::QueryWidget(`id(`table), `CurrentItem);
	    if(!IPsec::setCurrentConnection(name))
		continue;

            break;
        }
        else if(ret == `export_button) {
            ret = `export;
	    break;
	}
        else if(ret == `next || ret == `back) {
            break;
        }
        else {
            y2error("unexpected retcode: %1", ret);
            continue;
        }
    }

    return ret;
}

term CertificateOverviewContents(term header, list content)
{
    string caption = _("Certificate Overview");

    term contents = `VBox(
	`RadioButtonGroup(`id(`rbg),
	    `HBox(
		`RadioButton(`id(`cacerts), `opt(`notify), _("CA Certificates")),
		`RadioButton(`id(`certs), `opt(`notify), _("Certificates")),
		`RadioButton(`id(`crls), `opt(`notify), _("CRLs")),
		`RadioButton(`id(`keys), `opt(`notify), _("Keys"))
	    )
	),
	`HBox(
	    `Table(`id(`table), `opt(`notify), header, content),
	    `VBox(
		`PushButton(`id(`imp), _("&Import")),
		`PushButton(`id(`del), _("&Delete"))
	    )
	)
    );

    Wizard::SetContentsButtons(caption, contents, HELPS["c1"]:"",
	    Label::BackButton(), Label::NextButton());
}

void setCertContents()
{
    // DN is a X509 property called "Distinguised Name". Probably not
    // translatable. 'N' stands for new.
    CertificateOverviewContents(`header(_("N"), _("File"), _("DN")), IPsec::CertOverview());
    UI::ChangeWidget(`id(`certs), `Value, true);
    UI::SetFocus(`id(`imp));

}
void setCACertContents()
{
    // DN is a X509 property called "Distinguised Name". Probably not
    // translatable. 'N' stands for new.
    CertificateOverviewContents(`header(_("N"), _("File"), _("DN")), IPsec::CACertOverview());
    UI::ChangeWidget(`id(`cacerts), `Value, true);
    UI::SetFocus(`id(`imp));
}
void setCRLContents()
{
    // 'N' stands for new.
    CertificateOverviewContents(`header(_("N"), _("File"), _("Next Update"), _("Issuer")), IPsec::CRLOverview());
    UI::ChangeWidget(`id(`crls), `Value, true);
    UI::SetFocus(`id(`imp));
}
void setKeyContents()
{
    // 'N' stands for new.
    CertificateOverviewContents(`header(_("N"), _("File")), IPsec::KeyOverview());
    UI::ChangeWidget(`id(`keys), `Value, true);
    UI::SetFocus(`id(`imp));
}

any CertificateOverviewDialog ()
{
    symbol mode = `cacerts;
    setCACertContents();

    any ret = nil;
    while(true) {

	ret = UI::UserInput();

	/* abort? */
	if(ret == `abort || ret == `cancel) {
	    if(ReallyAbort()) break;
	    else continue;
	}
	else if(ret == `certs)
	{
	    setCertContents();
	    mode = (symbol) ret;
	}
	else if(ret == `cacerts)
	{
	    setCACertContents();
	    mode = (symbol) ret;
	}
	else if(ret == `keys)
	{
	    setKeyContents();
	    mode = (symbol) ret;
	}
	else if(ret == `crls)
	{
	    setCRLContents();
	    mode = (symbol) ret;
	}
	else if(ret == `imp)
	{
	    string file = UI::AskForExistingFile( IPsec::certimportdir,
		"*.pem *.PEM *.p12 *.P12 *.zip *.ZIP",
		_("Choose file to import"));

	    if(!IPsec::updateImportDir(file)) continue;

	    string msg = IPsecConfig::prepareImportFile(file);
	    if(msg != nil)
	    {
		Popup::Error(msg);
		continue;
	    }

	    if(mode == `cacerts)
	    {
		setCACertContents();
	    }
	    else if(mode == `certs)
	    {
		setCertContents();
	    }
	    else if(mode == `keys)
	    {
		setKeyContents();
	    }
	    else if(mode == `crls)
	    {
		setCRLContents();
	    }

	    continue;
	}
	else if(ret == `del)
	{
	    string val = (string) UI::QueryWidget(`id(`table), `CurrentItem);
	    if(val == nil)
		continue;

	    // %1 = filename
	    if(Popup::YesNo(sformat(_("Delete %1?"), val)) == false)
		continue;

	    if(mode == `cacerts)
	    {
		IPsec::deleteCACert(val);
		setCACertContents();
	    }
	    else if(mode == `certs)
	    {
		IPsec::deleteCert(val);
		setCertContents();
	    }
	    else if(mode == `keys)
	    {
		IPsec::deleteKey(val);
		setKeyContents();
	    }
	    else if(mode == `crls)
	    {
		IPsec::deleteCRL(val);
		setCRLContents();
	    }
	}
        else if(ret == `next) {
	    string msg = IPsecConfig::finishImport();
	    if(msg != nil)
	    {
		if(!Popup::ContinueCancel(msg))
		    continue;
	    }

            break;
        }
        else if(ret == `back) {
	    IPsecConfig::cleanupImport();
            break;
        }
        else {
            y2error("unexpected retcode: %1", ret);
            continue;
        }
    }

    return ret;
}

term ExpertConnectionContents()
{
    term header = `header(_("Key"), _("Value"));

    list content = IPsec::getCurrentConnectionItems();

    string caption = _("Configuration Options");

    term contents = `VBox(
	`HBox(
	    `Table(`id(`table), `opt(`notify), header, content),
	    `VBox(
		`PushButton(`id(`add), _("&Add")),
		`PushButton(`id(`edit), _("&Edit")),
		`PushButton(`id(`delete), _("&Delete"))
	    )
	)
    );

    Wizard::SetContentsButtons(caption, contents, HELPS["advanced"]:"",
	    Label::BackButton(), Label::NextButton());
}

list KeyValuePopup(string headline)
{
    UI::OpenDialog(
	`VBox(
	    `Heading(headline),
	    `TextEntry(`id(`key), _("Key:")),
	    `TextEntry(`id(`value), _("Value:")),
	    `VSpacing(0.5),
	    `HBox(
		`PushButton(`id(`ok), Label::OKButton()),
		`PushButton(`id(`cancel), Label::CancelButton())
	    )
	)
    );

    if( UI::UserInput() != `ok)
    {
	UI::CloseDialog();
	return nil;
    }

    string key = (string) UI::QueryWidget(`id(`key), `Value);
    string value = (string) UI::QueryWidget(`id(`value), `Value);

    UI::CloseDialog();

    return [ key, value ];
}

string TextEntryPopup(string headline, string txtlabel, string value)
{
    UI::OpenDialog(
	`VBox(
	    `Heading(headline),
	    `TextEntry(`id(`value), txtlabel, value),
	    `VSpacing(0.5),
	    `HBox(
		`PushButton(`id(`ok), `opt(`default), Label::OKButton()),
		`PushButton(`id(`cancel), Label::CancelButton())
	    )
	)
    );

    if( UI::UserInput() != `ok)
    {
	UI::CloseDialog();
	return nil;
    }

    string value = (string) UI::QueryWidget(`id(`value), `Value);

    UI::CloseDialog();

    return value;
}

any ConnectionExpertDialog ()
{
    symbol mode = `cacerts;

    ExpertConnectionContents();

    any ret = nil;
    while(true) {

	ret = UI::UserInput();

	/* abort? */
	if(ret == `abort || ret == `cancel) {
	    if(ReallyAbort()) break;
	    else continue;
	}
	else if(ret == `delete)
	{
	    string key = (string) UI::QueryWidget(`id(`table), `CurrentItem);
	    if(key == nil)
		continue;

	    if(Popup::YesNo(sformat(_("Delete %1?"), key)) == false)
		continue;

	    IPsec::current_connection = (map<string,string>) remove(IPsec::current_connection, key);

	    ExpertConnectionContents();

	}
	else if(ret == `add)
	{
	    list keyval = KeyValuePopup(_("Enter Key and Value"));

	    if(keyval == nil || size(keyval) != 2)
		continue;

	    string key = keyval[0]:"";
	    string value = keyval[1]:"";

	    if(key == "" || value == "")
		continue;

	    IPsec::current_connection[key] = value;

	    ExpertConnectionContents();

	}
	else if(ret == `edit)
	{
	    string key = (string) UI::QueryWidget(`id(`table), `CurrentItem);
	    if(key == nil)
		continue;

	    string val = TextEntryPopup(_("Enter New Value"), _("Value"), IPsec::current_connection[key]:"");

	    if(val == nil)
		continue;

	    IPsec::current_connection[key] = val;

	    ExpertConnectionContents();

	}
        else if(ret == `next || ret == `back) {
	    IPsec::commitChangedConnection();
            break;
        }
        else {
            y2error("unexpected retcode: %1", ret);
            continue;
        }
    }

    return ret;
}

/* EOF */
}
